plugins {
    id 'java-library' // Java library
    id 'maven-publish' // To publish into github
    id 'signing' // Required by maven central
    id 'org.sonatype.gradle.plugins.scan' version '2.8.3' // Sonatype scanner
}

group 'io.github.kamilszewc'
version '3.6'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.1'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    implementation 'io.github.kamilszewc:java-ansi-text-colorizer:1.4'
}

test {
    useJUnitPlatform()
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralTokenUser')) {
                    username = "$mavenCentralTokenUser"
                }
                if (project.properties.containsKey('mavenCentralTokenPassword')) {
                    password = "$mavenCentralTokenPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'tabulator'
            from components.java
            pom {
                name = 'Tabulator'
                description = 'Simple tables/lists output formater'
                url = 'https://github.com/kamilszewc/tabulator'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/tabulator.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/tabulator.git'
                    url = 'https://github.com/kamilszewc/tabulator.git'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
    sign configurations.archives
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}