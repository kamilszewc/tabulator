plugins {
    id 'java-library'
    id 'maven-publish' // To publish into github
    id 'signing' // Required by maven central
}

group 'com.computinglaboratory'
version '1.4'


repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "$mavenCentralUser"
                password = "$mavenCentralPassword"
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'tabulator'
            from components.java
            pom {
                name = 'Tabulator'
                description = 'Simple tables/lists output formater'
                url = 'https://github.com/computinglaboratory/tabulator'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/computinglaboratory/tabulator.git'
                    developerConnection = 'scm:git:https://github.com/computinglaboratory/tabulator.git'
                    url = 'https://github.com/computinglaboratory/tabulator.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
}