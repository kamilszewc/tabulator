plugins {
    id 'java-library' // Java library
    id 'maven-publish' // To publish into github
    id 'signing' // Required by maven central
    id 'org.sonatype.gradle.plugins.scan' version '2.5.5' // Sonatype scanner
}

group 'com.computinglaboratory'
version '3.0'

sourceCompatibility = '10'
targetCompatibility = '10'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    implementation 'com.computinglaboratory:java-ansi-text-colorizer:1.0'

}

test {
    useJUnitPlatform()
}

compileTestJava {
    sourceCompatibility = '15'
    targetCompatibility = '15'
}

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            try {
                credentials {
                    username = "$mavenCentralUser"
                    password = "$mavenCentralPassword"
                }
            } catch (Exception ex) {
                credentials {
                    username = " "
                    password = " "
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'tabulator'
            from components.java
            pom {
                name = 'Tabulator'
                description = 'Simple tables/lists output formater'
                url = 'https://github.com/kamilszewc/tabulator'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/tabulator.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/tabulator.git'
                    url = 'https://github.com/kamilszewc/tabulator.git'
                }
            }
        }

//        relocation(MavenPublication) {
//            from components.java
//            pom {
//                groupId = 'io.github.kamilszewc'
//                artifactId = 'tabulator'
//                version = '3.0'
//
//                name = 'Tabulator'
//                description = 'Simple tables/lists output formater'
//                url = 'https://github.com/kamilszewc/tabulator'
//                licenses {
//                    license {
//                        name = 'The Apache License, Version 2.0'
//                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
//                    }
//                }
//                developers {
//                    developer {
//                        id = 'kszewc'
//                        name = "Kamil Szewc"
//                        email = 'kamil.szewc@gmail.com'
//                    }
//                }
//                scm {
//                    connection = 'scm:git:git://github.com/kamilszewc/tabulator.git'
//                    developerConnection = 'scm:git:https://github.com/kamilszewc/tabulator.git'
//                    url = 'https://github.com/kamilszewc/tabulator.git'
//                }
//
//                distributionManagement {
//                    relocation {
//                        groupId = 'com.computinglaboratory'
//                        artifactId = 'tabulator'
//                        version = '3.0'
//                        message = 'Project moved to com.computinglaboratory:tabulator'
//                    }
//                }
//            }
//        }
    }
}

signing {
    sign publishing.publications.mavenJava
    sign configurations.archives
//    sign publishing.publications.relocation
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}